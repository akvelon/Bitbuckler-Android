/**
 * All rights reserved by Akvelon Inc.
 *
 * Created by Roman Gromov (roman.gromov@akvelon.com)
 * on 07 November 2020
 */

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
    id 'de.mannodermaus.android-junit5'
    id 'io.gitlab.arturbosch.detekt' version '1.19.0'
    id 'org.jlleitschuh.gradle.ktlint' version '10.2.0'
}

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.akvelon.bitbuckler"
        minSdkVersion 23
        targetSdkVersion 31
        versionCode 1
        versionName "1"

        // limit language resources used
        resConfigs "en", "ru"

        // Api endpoint
        buildConfigField "String", "BITBUCKET_API", '"https://api.bitbucket.org/"'

        // Authentication endpoint
        buildConfigField "String", "BITBUBCKET_AUTH_API", '"https://bitbucket.org/site/oauth2/"'
        buildConfigField "String", "CLIENT_ID", '"YOUR_CLIENT_ID"'
        buildConfigField "String", "CLIENT_SECRET", '"YOUR_CLIENT_SECRET"'

        // Socials
        buildConfigField "String", "EMAIL", '"YOUR_EMAIL"'
        buildConfigField "String", "GOOGLE_PLAY_LINK", '"PLAY_STORE_LINK_TO_YOUR_APP"'
        buildConfigField "String", "GOOGLE_ADMOB_ID", '"YOUR_GOOGLE_ADMOB_ID"'

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testOptions {
            unitTests {
                includeAndroidResources = true
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            debuggable true
        }
    }
    buildFeatures {
        viewBinding true
    }
    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError true
    }
    sourceSets.each {
        it.java.srcDirs += "src/$it.name/kotlin"
    }
}

repositories {
    mavenCentral()
}

detekt {
    config = files("$projectDir/../detekt-config.yml")
}

configurations {
    detekt
}

dependencies {

    // AndroidX
    def fragmentVersion = "1.4.0"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "androidx.fragment:fragment-ktx:$fragmentVersion"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "androidx.browser:browser:1.3.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.5.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.1"
    implementation "androidx.work:work-runtime-ktx:2.7.1"

    // ViewBinding
    implementation 'com.github.kirich1409:viewbindingpropertydelegate-noreflection:1.4.4'

    // Desugar
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.1.5'

    // Design Support
    implementation 'com.google.android.material:material:1.4.0'

    // MVP
    def moxyVersion = '2.2.2'
    implementation "com.github.moxy-community:moxy:$moxyVersion"
    implementation "com.github.moxy-community:moxy-ktx:$moxyVersion"
    implementation "com.github.moxy-community:moxy-androidx:$moxyVersion"
    implementation "com.github.moxy-community:moxy-material:$moxyVersion"
    kapt "com.github.moxy-community:moxy-compiler:$moxyVersion"

    // Navigation
    implementation "com.github.terrakok:cicerone:6.6"

    // Networking
    def retrofitVersion = '2.9.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    def okHttpVersion = '4.9.0'
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okHttpVersion"

    // DI
    def koinVersion = "3.1.4"
    implementation "io.insert-koin:koin-android:$koinVersion"
    implementation "io.insert-koin:koin-androidx-workmanager:$koinVersion"

    // ORM
    def roomVersion = "2.4.0"
    implementation "androidx.room:room-runtime:$roomVersion"
    kapt "androidx.room:room-compiler:$roomVersion"
    implementation "androidx.room:room-ktx:$roomVersion"
    testImplementation "androidx.room:room-testing:$roomVersion"

    // UI
    def glideVersion = "4.11.0"
    implementation "com.github.bumptech.glide:glide:$glideVersion"
    kapt "com.github.bumptech.glide:compiler:$glideVersion"
    implementation 'de.hdodenhof:circleimageview:3.1.0'
    implementation "com.sothree.slidinguppanel:library:3.4.0"
    implementation 'com.faltenreich:skeletonlayout:4.0.0'
    implementation 'com.caverock:androidsvg-aar:1.4'
    implementation 'com.google.android.flexbox:flexbox:3.0.0'

    //DateTime manipulation
    implementation 'net.danlew:android.joda:2.10.14'

    // Markwon
    def markwonVersion = '4.6.2'
    implementation "io.noties.markwon:core:$markwonVersion"
    implementation "io.noties.markwon:ext-strikethrough:$markwonVersion"
    implementation "io.noties.markwon:image-glide:$markwonVersion"
    implementation "io.noties.markwon:ext-tables:$markwonVersion"
    implementation "io.noties.markwon:linkify:$markwonVersion"
    implementation "io.noties.markwon:simple-ext:$markwonVersion"
    implementation "io.noties.markwon:editor:$markwonVersion"

    // Syntax Highlight
    implementation "com.pddstudio:highlightjs-android:1.5.0"

    // Play Core
    implementation 'com.google.android.play:core:1.10.2'
    implementation 'com.google.android.play:core-ktx:1.8.1'

    // Analytics
    def analyticsVersion = '21.1.0'
    implementation "com.google.firebase:firebase-analytics:$analyticsVersion"
    implementation "com.google.firebase:firebase-analytics-ktx:$analyticsVersion"
    implementation "com.google.firebase:firebase-crashlytics-ktx:18.2.12"

    implementation platform('com.google.firebase:firebase-bom:29.0.0')
    implementation 'com.google.firebase:firebase-firestore-ktx'

    // Remote config
    implementation platform('com.google.firebase:firebase-bom:30.3.1')
    implementation 'com.google.firebase:firebase-config-ktx'

    // Testing

    // JUnit
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.junit.platform:junit-platform-launcher:1.8.2'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testImplementation 'org.robolectric:robolectric:4.7.3'

    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"

    // Kaspresso
    androidTestImplementation 'com.kaspersky.android-components:kaspresso:1.3.0'
    debugImplementation "androidx.fragment:fragment-testing:$fragmentVersion"

    // MockWebServer
    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:4.3.1'

    // Kakao
    androidTestImplementation 'com.agoda.kakao:kakao:2.4.0'

    // Mockito
    def mockitoVersion = '3.9.0'
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    androidTestImplementation "org.mockito:mockito-android:$mockitoVersion"

    // AndroidX Tests
    androidTestUtil 'androidx.test:orchestrator:1.4.1'
    androidTestImplementation 'androidx.test:core-ktx:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'com.google.truth:truth:1.1.3'

    // Koin Tests
    androidTestImplementation "io.insert-koin:koin-test:$koinVersion"

    // Splash Screen
    implementation 'androidx.core:core-splashscreen:1.0.0-alpha01'

    // JetSec
    implementation "androidx.security:security-crypto:1.1.0-alpha03"

    //ad AdMob
    implementation "com.google.android.gms:play-services-ads:21.1.0"

    //google billing library
    implementation "com.android.billingclient:billing-ktx:5.0.0"
}

task lintProject {
    dependsOn 'ktlintCheck'
    dependsOn 'detekt'
    dependsOn 'lint'
}
